use crate::{Token, ParseError, StringId};
use crate::parser::grammar_helpers::*;
use crate::parser::ast::*;
use crate::parser::pos::{Span, Spanned};
use codespan::{ByteIndex, ByteOffset};
use std::sync::Arc;

grammar;

pub Program: Module = {
    Newlines? <Item> Newlines? => Module::new(vec![<>]),
    Newlines? <a: (<Item> Newlines?)+> <b: Item> Newlines? => {
        let mut v = vec![];
        v.extend(a);
        v.push(b);
        Module::new(v)
    }
}

Item: Arc<Item> = {
    Struct => Arc::new(Item::Struct(<>)),
    Def => Arc::new(Item::Def(<>))
}

Struct: Struct = {
    <l: @L> struct <name: Identifier> "{" Newlines? <fields: Fields> "}" <r: @R> =>
        Struct::new(name, fields, Span::from_indices(l, r))
}

Fields: Vec<Field> = {
    <fields: Delim<Field, COMMA>> => fields
}

COMMA = {
    "," Newlines?
}

COLON = {
    ":"
}

Field: Field = {
    <l: @L> <id: Identifier> ":" <ty: Type> <r: @R> => Field::new(id, ty, Span::from_indices(l, r))
}

Def: Def = {
    <l: @L> def <name: Identifier> <decl: FnDecl> <block: Block> <r: @R> =>
        Def::new(name, decl.0, decl.1, block, Span::from_indices(l, r))
}

FnDecl: (Vec<Field>, Option<Spanned<Type>>) = {
    <params: FnParams> <ty: RetTy?> => (params, ty)
}

FnParams: Vec<Field> = {
    "(" <Delim<Field, COMMA>> ")",
    "(" ")" => vec![]
}

RetTy: Spanned<Type> = {
    "->" <Type>
}

Type: Spanned<Type> = {
    <l: @L> <id: Identifier> <r: @R> => Spanned::from(Type::new(None, id), l, r),
    <l: @L> <mode: Mode> <id: Identifier> <r: @R> => Spanned::from(Type::new(Some(mode), id), l, r)
}

Mode: Spanned<Mode> = {
    <l: @L> own <r: @R> => Spanned::from(Mode::Owned, l, r),
    <l: @L> borrow <r: @R> => Spanned::from(Mode::Borrowed, l, r)
}

Block: Spanned<Block> = {
    <l: @L> "{" Newlines? <blocks: (<BlockItem> SEP Newlines?)*> "}" <r: @R> => {
        Block::spanned(blocks, Span::from_indices(l, r))
    },

    <l: @L> "{" <blocks: (<BlockItem> SEP Newlines?)*> <trailing: BlockItem> "}" <r: @R> => {
        let mut v = blocks;
        v.push(trailing);

        Block::spanned(v, Span::from_indices(l, r))
    }
}

BlockItem: BlockItem = {
    Decl => BlockItem::Decl(<>),
    Expr => BlockItem::Expr(<>)
}

Let: Let = {
    let <p: Pat> <ty: MaybeTypeAscription> <init: MaybeInitExpr> =>
        Let::new(p, ty, init)
}

#[inline]
MaybeTypeAscription: Option<Spanned<Type>> = {
    <ty: (":" <Type>)?> => ty
}

#[inline]
MaybeInitExpr: Option<Expression> = {
    <expr: ("=" <Expr>)?> => expr
}

Pat: Spanned<Pattern> = {
    <l: @L> "_" <r: @R> => Spanned::from(Pattern::Underscore, l, r),
    <l: @L> <id: Identifier> <r: @R> => Spanned::from(Pattern::Identifier(id, None), l, r),
    <l: @L> <mode: Mode> <id: Identifier> <r: @R> => Spanned::from(Pattern::Identifier(id, Some(mode)), l, r)
}

Decl: Declaration = {
    <l: Let> => Declaration::Let(l),
}

Expr: Expression = {
    Expr2
}

Expr1: Expression = {
    Block => unimplemented!("Expr::Block"),
    //ExprIf => unimplemented!("Expr::ExprIf"),
    //ExprIfLet => unimplemented!("Expr::ExprIfLet"),
    ConstructStruct => Expression::ConstructStruct(<>),
    Call => Expression::Call(<>),
    Identifier => Expression::Ref(<>),
    ExprAtom
}

Expr2: Expression = {
    Expr1,
    <left: Expr2> <op: ADD> <right: Expr1> => Expression::Binary(op, box left, box right)
}

ADD: Spanned<Op> = {
    <l: @L> "+" <r: @R> => Spanned::from(Op::Add, l, r)
}

ConstructStruct: ConstructStruct = {
    <l: @L> <name: Identifier> "{" <fields: Delim<ConstructField, COMMA>> "}" <r: @R> => ConstructStruct::new(name, fields, Span::from_indices(l, r))
}

Call: Spanned<Call> = {
    <l: @L> <callee: Callee> "(" <args: Args> ")" <r: @R> =>
        Spanned::from(Call::new(callee, args), l, r)
}

Args: Vec<Expression> = {
    Newlines? <Expr> Newlines? => vec![<>],
    Newlines? <a: (<Expr> Newlines? "," Newlines?)+> <b: Expr> Newlines? => {
        let mut v = vec![];
        v.extend(a);
        v.push(b);
        v
    }
}

Callee: Callee = {
    Identifier => Callee::Identifier(<>)
}

ConstructField: ConstructField = {
    Identifier => ConstructField::Shorthand(<>),
    <l: @L> <name: Identifier> ":" <ty: Type> <r: @R> => ConstructField::Longhand(Field::new(name, ty, Span::from_indices(l, r)))
}

//ExprIf: Expression = {
//    "if" Expr Block => unimplemented!("ExprIf::NoElse"),
//    "if" Expr Block "else" BlockOrIf => unimplemented!("ExprIf::Else")
//}

//ExprIfLet: If = {
//    "if" let Pat "=" Expr Block "else" BlockOrIf => unimplemented!("ExprIfLet::Else"),
//    "if" let Pat "=" Expr Block => unimplemented!("ExprIfLet::NoElse"),
//}

ExprAtom: Expression = {
    Lit => unimplemented!("ExprAtom::Lit"),
    PathExpr => unimplemented!("ExprAtom::PathExpr"),
    self => unimplemented!("ExprAtom::Self"),
    <l: @L> <s: string> <r: @R> => Expression::string(Spanned::from(s, l, r))
}

Lit: () = {
    "unimpl" => unimplemented!("Lit")
}

PathExpr: Path = {
    Delim<Identifier, "."> => unimplemented!("PathExpr::Dot"),
}

Identifier: Spanned<StringId> = {
    <l: @L> <id: identifier> <r: @R> => Spanned::from(id, l, r)
}

#[inline]
Path<SEP>: Path = {
    <Delim<Identifier, SEP>> => <>
}

// Delimeted list of E with optional trailing delimeter. Potentially
// empty.
Delim<T, SEP>: Vec<T> = {
    <v:(<T> SEP)+> => {
        v
    },

    <v:(<T> SEP)+> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

PrefixDelim<T, SEP>: Vec<T> = {
    <d: Delim<T, SEP>> => d,
    SEP <d: Delim<T, SEP>> => d
}

Comma<E> = Delim<E, ",">;

SEP: () = {
    ";",
    newline
}

Newlines: () = {
    newline+
}

extern {
    type Location = ByteIndex;
    type Error = ParseError;

    enum Token {
        "_" => Token::Underscore,
        "{" => Token::CurlyBraceOpen,
        "}" => Token::CurlyBraceClose,
        "(" => Token::ParenOpen,
        ")" => Token::ParenClose,
        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "," => Token::Comma,
        "=" => Token::Equals,
        "->" => Token::ThinArrow,
        "::" => Token::DoubleColon,
        "." => Token::Period,
        "+" => Token::OpAdd,
        def => Token::KeywordDef,
        let => Token::KeywordLet,
        struct => Token::KeywordStruct,
        "if" => Token::KeywordIf,
        "else" => Token::KeywordElse,
        own => Token::KeywordOwn,
        borrow => Token::KeywordBorrow,
        self => Token::KeywordSelf,
        while => Token::KeywordWhile,
        loop => Token::KeywordLoop,
        "for" => Token::KeywordFor,
        "unimpl" => Token::Unimplemented,
        identifier => Token::Identifier(<StringId>),
        string => Token::StringLiteral(<StringId>),
        TemplateFragment => Token::StringFragment(<StringId>),
        EndTemplate => Token::EndString(<StringId>),
        newline => Token::Newline
    }
}
